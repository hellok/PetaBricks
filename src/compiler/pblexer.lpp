/*****************************************************************************
 *  Copyright (C) 2008-2011 Massachusetts Institute of Technology            *
 *                                                                           *
 *  Permission is hereby granted, free of charge, to any person obtaining    *
 *  a copy of this software and associated documentation files (the          *
 *  "Software"), to deal in the Software without restriction, including      *
 *  without limitation the rights to use, copy, modify, merge, publish,      *
 *  distribute, sublicense, and/or sell copies of the Software, and to       *
 *  permit persons to whom the Software is furnished to do so, subject       *
 *  to the following conditions:                                             *
 *                                                                           *
 *  The above copyright notice and this permission notice shall be included  *
 *  in all copies or substantial portions of the Software.                   *
 *                                                                           *
 *  THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY                *
 *  KIND, EXPRESS OR IMPLIED, INCLUDING BUT NOT LIMITED TO THE               *
 *  WARRANTIES OF MERCHANTABILITY, FITNESS FOR A PARTICULAR PURPOSE AND      *
 *  NONINFRINGEMENT. IN NO EVENT SHALL THE AUTHORS OR COPYRIGHT HOLDERS BE   *
 *  LIABLE FOR ANY CLAIM, DAMAGES OR OTHER LIABILITY, WHETHER IN AN ACTION   *
 *  OF CONTRACT, TORT OR OTHERWISE, ARISING FROM, OUT OF OR IN CONNECTION    *
 *  WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE SOFTWARE           *
 *                                                                           *
 *  This source code is part of the PetaBricks project:                      *
 *    http://projects.csail.mit.edu/petabricks/                              *
 *                                                                           *
 *****************************************************************************/
%option stack
%option nostdinit
%option noyywrap
%option noyy_top_state
%option nounput
%option prefix="pb"
%option outfile="lex.yy.c"
%option bison-bridge
%option bison-locations
%option yylineno

%{

#include "codegenerator.h"
#include "formula.h"
#include "matrixdef.h"
#include "transform.h"
#include "userrule.h"

#include "common/jassert.h"
#include "common/jconvert.h"
#include "common/srcpos.h"

#include <stdio.h>
#include <string>
#include <string.h>
#include <sstream>

#include "pbparser.h"

using namespace std;

void pbparser_addRefPoolItem(jalib::JRefCounted* i);//pbparser.ypp

static std::stringstream theParseLog;

static const char* allocPoolString(const char* str){
  struct StrStorage : public std::string, public jalib::JRefCounted {
    StrStorage(const char* s) : std::string(s) {}
  };
  StrStorage* s = new StrStorage(str);
  pbparser_addRefPoolItem(s);
  return s->c_str();
}

static void recordLitCode(){
  std::string str = theParseLog.str().c_str();
  theParseLog.str("");
  petabricks::CodeGenerator::theFilePrefix() << std::string(str.begin(), str.end()-2);
}

std::string pbfilename;
int rulebodystart=0;
int pbcolno=0;
int pblastlineno=-1;

static void handleCppLine(const char* s){
  const char* n_begin = s+2;
  const char* n_end = n_begin;
  while(*n_end>='0' && *n_end<='9') ++n_end;
  const char* filename=n_end+1;
  std::string n(n_begin, n_end);
  //JTRACE("LINENO")(n)(filename);
  pblineno = jalib::StringToInt(n)-1;
  pbfilename = filename;
}

#define YY_USER_ACTION \
  pblval.str=allocPoolString(yytext);                              \
  theParseLog<<yytext;                                             \
  SRCPOS_FILL(*yylloc, pbfilename, pblineno, pblastlineno, pbcolno)

#define RETURN theParseLog.str(""); return

static int theNestCount=0;

%}

%x transform rule comment linecomment string litcode

PASS_CHARS [=.<>,*/()[\]^+-]

%%
<INITIAL>{
  "main"            RETURN KW_MAIN;
  "memoized"        RETURN KW_MEMOIZED;
  "transform"       RETURN KW_TRANSFORM;
  "template"        RETURN KW_TEMPLATE;
  "generator"       RETURN KW_GENERATOR;
  "gen"             RETURN KW_GENERATOR;
  "config"          RETURN KW_CONFIG;
  "param"           RETURN KW_PARAM;
  "parameter"       RETURN KW_PARAM;
  "tunable"         RETURN KW_TUNABLE;
  "accuracy_metric" RETURN KW_ACCURACYMETRIC;
  "accuracy_bins"   RETURN KW_ACCURACYBINS;
  "accuracy_variable" RETURN KW_ACCURACYVARIABLE;
  "{"               yy_push_state(transform); RETURN '{';
  "%{"              yy_push_state(litcode); theParseLog.str("");
}

<litcode>{
  [^%]*            /*nothing*/
  [%][^}]          /*nothing*/
  "%}"             {
      yy_pop_state();
      recordLitCode();
  }
}

<transform>{
  "where"          RETURN KW_WHERE;
  "priority"       RETURN KW_PRIORITY;
  "primary"        RETURN KW_PRIMARY;
  "secondary"      RETURN KW_SECONDARY;
  "rotatable"      RETURN KW_ROTATABLE;
  "recursive"      RETURN KW_RECURSIVE;
  "duplicate"      RETURN KW_DUPLICATE;
  "rule"           RETURN KW_RULE;
  "{"              {
                     yy_push_state(rule); 
                     theNestCount=1; 
                     theParseLog.str("");
                     theParseLog << "# " << pblineno << " " << pbfilename << "\n";
                     return *yytext;
                   }
  "}"              yy_pop_state();      RETURN *yytext;
}

<INITIAL,transform>{
  "to"              RETURN KW_TO;
  "from"            RETURN KW_FROM;
  "using"           RETURN KW_THROUGH;
  "through"         RETURN KW_THROUGH;
  "<="              RETURN KW_LE;
  ">="              RETURN KW_GE;
  "=="              RETURN KW_EQ;
  "&&"              RETURN KW_AND;
  "and"             RETURN KW_AND;
  "||"              RETURN KW_OR;
  "or"              RETURN KW_OR;
  [0-9]+                      RETURN TOK_INTEGER;
  [+-]?[0-9]+[.][0-9]+        RETURN TOK_FLOAT;
  [+-]?[0-9.]+[eE][+-]?[0-9]+ RETURN TOK_FLOAT;
  [a-zA-Z_][a-zA-Z0-9_]*      RETURN IDENT;
  {PASS_CHARS}      RETURN yytext[0];
  [/][*]            yy_push_state(comment);
  [/][/]            yy_push_state(linecomment);
  ^[#]              yy_push_state(linecomment);
}

<rule>{
  [/][*]           yy_push_state(comment);
  [/][/]           yy_push_state(linecomment);
  ^[#]             yy_push_state(linecomment);
  "{"              ++theNestCount;
  ["]              yy_push_state(string);
  "}"              { 
                      if(--theNestCount==0){
                        yy_pop_state();
                        pblval.str=allocPoolString(theParseLog.str().c_str());
                        RETURN TOK_RULEBODY;
                      }
                   }
  [^*/{}"]+        /*nothing*/
  .                /*nothing*/
}

<comment>{
  "*/"             yy_pop_state();
  [^*]+            /*nothing*/
  .                /*nothing*/
}

<linecomment>{
  [\n]           yy_pop_state();
  [^\n]+         /*nothing*/
}

<string>{
  ["]            yy_pop_state();
  [^"]           /*nothing*/
  [\\]["]        /*nothing*/
}

<*>{
  ^[#][ ][0-9]+[ ][^\n]*   handleCppLine(yytext);
  [ \t\r\n]      /* whitespace */;
  .              JASSERT(false)(yytext)(yylineno).Text("Unhandled input");
}

%%


