#ifndef RADIXSORT4_PBCC
#define RADIXSORT4_PBCC

#include "../simple/copy.pbcc"

#ifndef SORTSUBARRAY
#define SORTSUBARRAY RadixsortSubArray
#endif

#define NUM_BITS_PER_RADIX 2
#define NUM_BUCKETS (1 << NUM_BITS_PER_RADIX)

%{

typedef          long long int64;
typedef unsigned long long uint64;

int getBits(double in, int position)
{
  union {
    double d;
    uint64 u;
  };
  d = in;
  uint64 inInt = u;
//printf("in:    %llx\n", in);
//printf("inInt: %llx\n", inInt);
  uint64 mask = -int64(inInt >> 63) | ((uint64) 1 << 63);
  inInt ^= mask;
  return (inInt & ((uint64) (NUM_BUCKETS - 1) << position)) >> position;
}

void Radix(MatrixRegion1D &out, MatrixRegion1D &in, int *buckets, int pos)
{
  int i;
  for (i = 0; i < in.size(0); i++) {
//    printf("Putting %g into bucket %d\n", in.cell(i), getBits(in.cell(i), pos));  
    buckets[getBits(in.cell(i), pos) + 2]++;
  }
/*
  printf("bucket hist:  (%d", buckets[2]);
  for (i = 3; i < NUM_BUCKETS + 2; i++) {
    printf(", %d", buckets[i]);
  }
  printf(")\n");
*/
  for (i = 3; i < NUM_BUCKETS + 2; i++) {
    buckets[i] += buckets[i - 1];
  }

  for (i = 0; i < in.size(0); i++) {
//    printf("copying %g into position %d\n", in.cell(i), buckets[getBits(in.cell(i), pos) + 1]);
    out.cell(buckets[getBits(in.cell(i), pos) + 1]++) = in.cell(i);
  }
/*
  printf("bucket start: (%d", buckets[0]);
  for (i = 1; i < NUM_BUCKETS; i++) {
    printf(", %d", buckets[i]);
  }
  printf(")\n");
*/
}

%}

transform RadixsortSubArray
from IN[n], Position
to OUT[n], TEMP[n]
{
  to (OUT out, TEMP temp) from (IN in, Position pos)
  {
//    printf("RadixsortSubArray called\n");
    int buckets[NUM_BUCKETS + 2];
    memset(buckets, 0, sizeof(buckets));
    Copy1D(temp, out);
    Radix(out, temp, buckets, (int) pos);
    if ((int) pos > 0) {
      for (int i = 0; i < NUM_BUCKETS; i++) {
        if (buckets[i + 1] > buckets[i] + 1) {
          spawn SORTSUBARRAY(out.region(buckets[i], buckets[i + 1]), 
                            temp.region(buckets[i], buckets[i + 1]),
                            in.region(buckets[i], buckets[i + 1]), 
                            (int) pos - NUM_BITS_PER_RADIX);
        }
      }
    }
  }
}

transform Radixsort
from IN[n]
to OUT[n]
{
  to (OUT out) from (IN in)
  {
    MatrixRegion1D temp = MatrixRegion1D::allocate(n);
    Copy1D(out, in);
    RadixsortSubArray(out, temp, in, 64 - NUM_BITS_PER_RADIX);
  }
}

#endif // RADIXSORT4_PBCC

